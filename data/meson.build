conf = configuration_data()
conf.set('bindir', join_paths(prefix, get_option('bindir')))

dbus_dep = dependency ('dbus-1')
session_bus_services_dir = get_option('session-bus-services-dir')

if session_bus_services_dir == ''
   session_bus_services_dir = dbus_dep.get_pkgconfig_variable('session_bus_services_dir')
endif

configure_file(
    input: 'com.endlessm.Clubhouse.service.in',
    output: 'com.endlessm.Clubhouse.service',
    install: true,
    install_dir: session_bus_services_dir,
    configuration: conf
)

gnome.compile_resources(
    'eos-clubhouse',
    files('com.endlessm.Clubhouse.gresource.xml'),
    source_dir : [
        '.',
        './assets'
    ],
    gresource_bundle: true,
    install: true,
    install_dir: clubhouse_data_dir
)

install_data(
    'eos-clubhouse.desktop',
    install_dir: join_paths(data_dir, 'applications')
)

install_data(
    'com.endlessm.Clubhouse.appdata.xml',
    install_dir: join_paths(data_dir, 'appdata')
)

characters_dir_name = 'characters'
items_dir_name = 'items'
episodes_dir_name = 'episodes'
quests_strings_dir_name = 'quests_strings'
tools_dir_name = 'tools'

data_dir_paths = [characters_dir_name, items_dir_name, episodes_dir_name, quests_strings_dir_name, tools_dir_name]

foreach path : data_dir_paths
    install_subdir(path, install_dir: clubhouse_data_dir)
endforeach

quests_items_csv_name = 'quests_items.csv'
episodes_csv_name = 'episodes.csv'

data_file_paths = [quests_items_csv_name, episodes_csv_name]

foreach path : data_file_paths
    install_data(path, install_dir: clubhouse_data_dir)
endforeach

# For later substitution in the config file
characters_dir = join_paths(clubhouse_data_dir, characters_dir_name)
item_icons_dir = join_paths(clubhouse_data_dir, items_dir_name)
episodes_dir = join_paths(clubhouse_data_dir, episodes_dir_name)
episodes_csv = join_paths(clubhouse_data_dir, episodes_csv_name)
quests_strings_dir = join_paths(clubhouse_data_dir, quests_strings_dir_name)
quests_items_csv = join_paths(clubhouse_data_dir, quests_items_csv_name)
